# Maintainer: BurningC4 <wangweichen1995@hotmail.com>

pkgname=kodi-addon-pvr-dvblink
pkgbase=${pkgname}
pkgver=20.3.0
pkgrel=1
pkgdesc="Kodi's DVBLink client addon"
arch=('i686' 'x86_64' 'armv6h' 'armv7h' 'aarch64')
url="https://github.com/kodi-pvr/pvr.dvblink"
license=('GPL')
groups=('kodi-addons')
makedepends=('cmake' 'kodi-platform' 'git' 'kodi-dev')
depends=('kodi' 'p8-platform')
source=("pvr.dvblink.tar.gz::https://github.com/kodi-pvr/pvr.dvblink/archive/5bc0e5ef52f55f61351c03dfc20b64754c2e04b3.tar.gz")
sha512sums=('f0aa784f5ea0426cabc0a60ccd28bced7c02eafb2ed3a697c700889d8a15d9fe89f63e1b0a9d98e94a3b8d71a0a594dfcd3d7143abeec0b7811eee014eefc837')

_prefix='/usr'

build() {
  msg "Starting make..."

  cd "$srcdir/pvr.dvblink-5bc0e5ef52f55f61351c03dfc20b64754c2e04b3"
  mkdir -p build && pushd build
  cmake \
    -DCMAKE_INSTALL_PREFIX=$_prefix \
    -DCMAKE_BUILD_TYPE=Release \
    ..
  make
  popd
}

package() {
  cd "$srcdir/pvr.dvblink-5bc0e5ef52f55f61351c03dfc20b64754c2e04b3/build"
  make DESTDIR="$pkgdir" install
}
